/* Plugin generated by AMXX-Studio */

#include < amxmodx >
#include < fakemeta >
#include < cstrike >
#include < CC_ColorChat >

#define PLUGIN "Name Status"
#define VERSION "0.1.0"

#define	MAX_STATUS	9

new const g_szStatus[ MAX_STATUS ][ ] =
{
	"",
	"[DND]",
	"[ON]",
	"[Idle]",
	"[Pe forum]",
	"[Pe skype]",
	"[Pe mess]",
	"[Pe facebook]",
	"[AFK]"
	
}
		
new g_szName[ 33 ][ 32 ];

public plugin_init( )
{
	register_plugin( PLUGIN, VERSION, "Askhanar" );

	register_clcmd( "say","ClCmdSay");
	register_clcmd( "say_team","ClCmdSayTeam");
	
	register_forward( FM_ClientUserInfoChanged, "fwd_ClientUserInfoChangedPre", true );
	// Add your code here...
}


public client_putinserver( id )	get_user_name( id, g_szName[ id ], sizeof( g_szName[ ] ) -1 );

public ClCmdSay( id )
{
	
	if( is_user_bot( id ) )
		return PLUGIN_CONTINUE;
	
	static szChat[ 192 ];
	read_args( szChat, sizeof( szChat ) - 1 );
	remove_quotes( szChat );
	
	if( equali( szChat,"" ) )
		return PLUGIN_CONTINUE;
	
	else if( equali( szChat, "/status" ) )
	{
		new iMenu = menu_create( "\rAlege un status\w:", "StatusHandler" );
	
		new szKey[ 10 ];
		for( new i = 1; i < MAX_STATUS; i++ )
		{
			num_to_str( i, szKey, sizeof( szKey ) -1 );
			menu_additem( iMenu, g_szStatus[ i ], szKey, 0 );
			
		}
			
		menu_setprop( iMenu, MPROP_EXIT, MEXIT_ALL );
		menu_display( id, iMenu, 0 );
	}
	else if( equali( szChat, "/delstatus" ) )
	{
		get_user_name( id, g_szName[ id ], sizeof( g_szName[ ] ) -1 );
		ReplaceStatusInName( id );
		client_cmd( id, "name ^"%s^"", g_szName[ id ] );
	}
	
	switch( cs_get_user_team( id ) )
	{
		case CS_TEAM_T: 	ColorChat( 0, RED,"^x03%s %s^x01: %s", is_user_alive( id ) ? "" : "*Mort*", g_szName[ id ],  szChat );
		case CS_TEAM_CT:	ColorChat( 0, BLUE,"^x03%s %s^x01: %s", is_user_alive( id ) ? "" : "*Mort*", g_szName[ id ],  szChat );
		case CS_TEAM_SPECTATOR:	ColorChat( 0, GREY,"^x03%s^x01: %s",g_szName[ id ],  szChat );		
	}
	
	
	return PLUGIN_HANDLED;
	
}

public ClCmdSayTeam( id )
{
	
	if( is_user_bot( id ) )
		return PLUGIN_CONTINUE;
	
	static szChat[ 192 ];
	read_args( szChat, sizeof( szChat ) - 1 );
	remove_quotes( szChat );
	
	if( equali( szChat,"" ) )
		return PLUGIN_CONTINUE;
	
	
	static iPlayers[ 32 ], iPlayer;
	static iPlayersNum;

	get_players( iPlayers, iPlayersNum, "ch" );		
	for( new i = 0 ; i < iPlayersNum ; i++ )
	{
		iPlayer = iPlayers[ i ];
		
		if( cs_get_user_team( id ) == cs_get_user_team( iPlayer ) )
		{
			switch( cs_get_user_team( iPlayer ) )
			{
				case CS_TEAM_T: 	ColorChat( iPlayer, RED, "^x03(Terrorist)%s %s^x01: %s",is_user_alive( id ) ? "" : "*Mort*",  g_szName[ id ],  szChat );
				case CS_TEAM_CT:	ColorChat( iPlayer, BLUE, "^x03(Counter-Terrorist)%s %s^x01: %s",is_user_alive( id ) ? "" : "*Mort*",  g_szName[ id ],  szChat );
				case CS_TEAM_SPECTATOR:	ColorChat( iPlayer, GREY, "^x03(Spectator)%s^x01: %s",  g_szName[ id ],  szChat );
			}
		}
	}
	return PLUGIN_HANDLED;
}

public StatusHandler( id, iMenu, iItem)
{
	if( iItem == MENU_EXIT )
	{
		menu_destroy( iMenu );
		return;
	}
	
	static _access, szInfo[ 4 ], callback;
	menu_item_getinfo( iMenu, iItem, _access, szInfo, sizeof( szInfo ) -1, _, _, callback );
	menu_destroy( iMenu );
	
	ReplaceStatusInName( id );
	client_cmd( id, "name ^"%s %s^"", g_szStatus[ str_to_num( szInfo ) ], g_szName[ id ] );
	
}

public fwd_ClientUserInfoChangedPre( id, szInfoKey )
{
	if( !is_user_connected( id ) )
		return;
	
	engfunc( EngFunc_InfoKeyValue, szInfoKey, "name", g_szName[ id ], sizeof( g_szName[ ] ) -1 );
	ReplaceStatusInName( id );
}


ReplaceStatusInName( id )
{
	
	for( new i = 1; i < MAX_STATUS; i++ )
	{
		if( containi( g_szName[ id ], g_szStatus[ i ] ) != -1 )
		{
			replace_all( g_szName[ id ], sizeof( g_szName[ ] ) -1, g_szStatus[ i ], "" );
			trim( g_szName[ id ] );
		}
	}
}
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1033\\ f0\\ fs16 \n\\ par }
*/